project(":tioga-push-server-grizzly") {

    apply plugin: 'distribution'

    version = tiogaAppVersion

    dependencies {
        compile(project(":tioga-push-engine"))

        compile("org.tiogasolutions.lib:tioga-lib-spring:+")

        compile("org.glassfish.jersey.ext:jersey-spring3:2.22.2")

        compile("org.tiogasolutions.runners:tioga-runners-grizzly:+")

        // required by Jersey-Spring's stupid attempt to check for a ServletContext
        compile("javax.servlet:javax.servlet-api:3.1.0")

        testCompile("org.testng:testng:6.8.21")
    }

    File procfile = file("$buildDir/aws/Procfile");
    task createProcfile << {
        procfile.parentFile.mkdirs()
        FileWriter writer = new FileWriter(procfile);
        writer.write("web: java -cp ./lib/* org.tiogasolutions.push.server.grizzly.PushServer\n");
        writer.close()
    }

    distributions {
        main {
            contents {
                into("lib") {
                    from(jar)
                    from(project.configurations.runtime)
                }
                from(procfile, createProcfile)
            }
        }
    }

    jar {
        into("libs") {
            from configurations.runtime
        }
        manifest {
            attributes(
                    "Implementation-Version": project.version,
                    "Main-Class": "org.tiogasolutions.push.server.grizzly.PushServer",
                    "Class-Path": configurations.compile.collect { "./libs/" + it.getName() }.join(" ")
            )
        }
    }

    task pws(type: Copy, dependsOn: assemble) {
        from "./build/libs/tioga-push-server-grizzly-${project.version}.jar"
        into "./build/pws/"
        rename "tioga-push-server-grizzly-${project.version}.jar", "tioga-push-server-grizzly.jar"
    }

    apply(from: urlCache.get("https://raw.githubusercontent.com/tioga/build-scripts/master/build-config-deployer.gradle"))
}
